# Maintainer: ajs124 <ajs@online.de>
# Contributor: Devin Cofer <ranguvar{AT]archlinux[DOT}us>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Frederic Bezies <fredbezies@gmail.com>
# Contributor: G. Richard Bellamy <rbellamy@pteradigm.com>

pkgname=('qemu-git' 'libcacard-git')
_gitname='qemu'
pkgver=2.2.r34450.gcd2d554
pkgrel=1
arch=('i686' 'x86_64')
license=('GPL2' 'LGPL2.1')
url="http://wiki.qemu.org/Index.html"
makedepends=('pixman' 'libjpeg' 'libpng' 'sdl' 'alsa-lib' 'nss' 'glib2'
            'gnutls>=2.4.1' 'bluez-libs' 'vde2' 'util-linux' 'curl' 'libsasl'
            'libgl' 'libpulse' 'seabios' 'libcap-ng' 'libaio' 'libseccomp'
            'libiscsi' 'libcacard' 'spice' 'spice-protocol' 'python2'
            'usbredir')
optdepends=('ovmf-bin: UEFI Firmware (OVMF) with Secure Boot Support - for Virtual Machines (QEMU)')
options=(!strip)
source=('git://git.qemu.org/qemu.git'
        '65-kvm.rules'
        'qemu.install')
sha256sums=('SKIP'
            '9c8a15c34461a9481a34ca9e0ab4ae3825eabe8fd863227f2445325413cd755c'
            '9970c3050e8dc6153c5955d018d114f9fcbc091843b85f9e7b247eb28f09ba10')

pkgver() {
  cd "${srcdir}/${_gitname}"
  echo "2.2.r$(git rev-list --count master).g$(git log -1 --format="%h")"
}

build() {
  cd "${_gitname}"
  # qemu vs. make 4 == bad
  export ARFLAGS="rv"

  # http://permalink.gmane.org/gmane.comp.emulators.qemu/238740
  # gtk gui breaks keymappings at the moment
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --audio-drv-list='pa alsa sdl' \
    --python=/usr/bin/python2 \
    --smbd=/usr/bin/smbd \
    --enable-docs \
    --libexecdir=/usr/lib/qemu \
    --disable-gtk \
    --enable-linux-aio \
    --enable-seccomp \
    --enable-spice \
    --localstatedir=/var \
    --enable-tpm \
    --disable-werror \
    --disable-strip \
    --enable-numa

  make V=99
}

package_qemu-git() {
  pkgdesc="A generic and open source processor emulator which achieves a good emulation speed by using dynamic translation."
  depends=('pixman' 'libjpeg' 'libpng' 'sdl' 'alsa-lib' 'nss' 'glib2'
         'gnutls>=2.4.1' 'bluez-libs' 'vde2' 'util-linux' 'curl' 'libsasl'
         'libgl' 'libpulse' 'seabios' 'libcap-ng' 'libaio' 'libseccomp'
         'libiscsi' 'libcacard' 'spice' 'usbredir')
  conflicts=('qemu' 'qemu-git' 'kvm' 'kvm-git' 'qemu-spice')
  provides=('qemu')

  backup=('etc/qemu/target-x86_64.conf')
  install='qemu.install'

  cd "${_gitname}"
  make DESTDIR="${pkgdir}" libexecdir="/usr/lib/qemu" install

  # provided by seabios package
  rm "${pkgdir}/usr/share/qemu/bios.bin"
  rm "${pkgdir}/usr/share/qemu/acpi-dsdt.aml"
  rm "${pkgdir}/usr/share/qemu/q35-acpi-dsdt.aml"
  rm "${pkgdir}/usr/share/qemu/bios-256k.bin"
  rm "${pkgdir}/usr/share/qemu/vgabios-cirrus.bin"
  rm "${pkgdir}/usr/share/qemu/vgabios-qxl.bin"
  rm "${pkgdir}/usr/share/qemu/vgabios-stdvga.bin"
  rm "${pkgdir}/usr/share/qemu/vgabios-vmware.bin"

  # remove conflicting /var/run directory
  rm -r "${pkgdir}/var"

  # rules and tools
  sed 's/python/python2/g' -i "${srcdir}/qemu/scripts/kvm/kvm_stat"
  sed 's/python/python2/g' -i "${srcdir}/qemu/scripts/kvm/kvm_flightrecorder"
  sed 's/python/python2/g' -i "${srcdir}/qemu/scripts/tracetool.py"
  sed 's/python/python2/g' -i "${srcdir}/qemu/scripts/simpletrace.py"
  install -D -m644 "${srcdir}/65-kvm.rules" \
                   "${pkgdir}/usr/lib/udev/rules.d/65-kvm.rules"
  install -D -m755 "${srcdir}/qemu/scripts/kvm/kvm_stat" \
                   "${pkgdir}/usr/lib/qemu/kvm_stat"
  install -D -m755 "${srcdir}/qemu/scripts/kvm/kvm_flightrecorder" \
                   "${pkgdir}/usr/lib/qemu/kvm_flightrecorder"
  install -D -m755 "${srcdir}/qemu/scripts/tracetool.py" \
                   "${pkgdir}/usr/lib/qemu/tracetool.py"
  install -D -m755 "${srcdir}/qemu/scripts/simpletrace.py" \
                   "${pkgdir}/usr/lib/qemu/simpletrace.py"
  install -D -m755 "${srcdir}/qemu/scripts/cleanup-trace-events.pl" \
                   "${pkgdir}/usr/lib/qemu/cleanup-trace-events.pl"

  # bridge_helper needs suid
  # https://bugs.archlinux.org/task/32565
  chmod u+s "${pkgdir}/usr/lib/qemu/qemu-bridge-helper"

  # add sample config
  echo "allow br0" > ${pkgdir}/etc/qemu/bridge.conf.sample

  # strip scripts directory
  find "${pkgdir}/usr/src/linux-${_kernver}/scripts"  -type f -perm -u+w 2>/dev/null | while read binary ; do
    case "$(file -bi "$binary")" in
      *application/x-executable*) # Binaries
      /usr/bin/strip $STRIP_BINARIES "$binary";;
    esac
  done

  # remove libcacard files
  rm -rf ${pkgdir}/usr/include/cacard
  rm -rf ${pkgdir}/usr/lib/libcacard*
  rm -rf ${pkgdir}/usr/lib/pkgconfig/libcacard.pc
  rm -rf ${pkgdir}/usr/bin/vscclient
}

package_libcacard-git() {
  pkgdesc="Common Access Card (CAC) Emulation"
  options=('strip')
  depends=('nss' 'libaio' 'libcap-ng' 'libiscsi' 'curl' 'vde2' 'glib2')
  conflicts=('libcacard' 'libcacard-git')
  provides=('libcacard')
  mkdir -p ${pkgdir}/usr/bin
  mkdir -p ${pkgdir}/usr/lib/pkgconfig
  mkdir -p ${pkgdir}/usr/include/cacard
  cp -a ${srcdir}/${_gitname}/libcacard/*.h ${pkgdir}/usr/include/cacard/
  cp -a ${srcdir}/${_gitname}/.libs/libcacard.so* ${pkgdir}/usr/lib/
  cp -a ${srcdir}/${_gitname}/libcacard.pc ${pkgdir}/usr/lib/pkgconfig/
  cp -a ${srcdir}/${_gitname}/.libs/vscclient ${pkgdir}/usr/bin/
}
