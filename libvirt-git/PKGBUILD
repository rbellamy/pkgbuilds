# $Id: PKGBUILD 82286 2013-01-14 10:03:34Z spupykin $
# Maintainer: Arthur Borsboom <arthurborsboom@gmail.com>
# Maintainer: Jonas Heinrich <onny@project-insanity.org>
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jonathan Wiersma <archaur at jonw dot org>
# Contributor: G. Richard Bellamy <rbellamy@pteradigm.com>

_pkgver=1.2.14

pkgname=libvirt-git
pkgver=1.2.14.r18684.gecb9a53
pkgrel=1

pkgdesc="API for controlling virtualization engines (openvz,kvm,qemu,virtualbox,xen,etc)"

arch=('i686' 'x86_64')
url="http://libvirt.org/"
license=('LGPL')

depends=('e2fsprogs' 'gnutls' 'iptables' 'libxml2' 'parted' 'polkit' 'python2'
         'avahi' 'yajl' 'libpciaccess' 'udev' 'dbus' 'libxau' 'libxdmcp' 'libpcap'
         'curl' 'libsasl' 'libgcrypt' 'libgpg-error' 'openssl' 'libxcb' 'gcc-libs'
         'iproute2' 'libnl' 'libx11' 'numactl' 'gettext')
makedepends=('pkgconfig' 'lvm2' 'linux-api-headers' 'dnsmasq' 'lxc'
             'libiscsi' 'open-iscsi'
             'perl-xml-xpath' 'libxslt')
optdepends=('ebtables: required for default NAT networking'
            'dnsmasq: required for default NAT/DHCP for guests'
            'bridge-utils: for brigded networking'
            'openbsd-netcat: for remote management over ssh'
            'qemu'
            'radvd'
            'dmidecode'
            'ebtables'
            'pm-utils: host power management'
            'audit')

provides=('libvirt')
conflicts=('libvirt')

options=('emptydirs')

backup=('etc/conf.d/libvirt-guests'
        'etc/conf.d/libvirtd'
        'etc/libvirt/libvirt.conf'
        'etc/libvirt/libvirtd.conf'
        'etc/libvirt/lxc.conf'
        'etc/libvirt/nwfilter/allow-arp.xml'
        'etc/libvirt/nwfilter/allow-dhcp-server.xml'
        'etc/libvirt/nwfilter/allow-dhcp.xml'
        'etc/libvirt/nwfilter/allow-incoming-ipv4.xml'
        'etc/libvirt/nwfilter/allow-ipv4.xml'
        'etc/libvirt/nwfilter/clean-traffic.xml'
        'etc/libvirt/nwfilter/no-arp-ip-spoofing.xml'
        'etc/libvirt/nwfilter/no-arp-mac-spoofing.xml'
        'etc/libvirt/nwfilter/no-arp-spoofing.xml'
        'etc/libvirt/nwfilter/no-ip-multicast.xml'
        'etc/libvirt/nwfilter/no-ip-spoofing.xml'
        'etc/libvirt/nwfilter/no-mac-broadcast.xml'
        'etc/libvirt/nwfilter/no-mac-spoofing.xml'
        'etc/libvirt/nwfilter/no-other-l2-traffic.xml'
        'etc/libvirt/nwfilter/no-other-rarp-traffic.xml'
        'etc/libvirt/nwfilter/qemu-announce-self-rarp.xml'
        'etc/libvirt/nwfilter/qemu-announce-self.xml'
        'etc/libvirt/qemu-lockd.conf'
        'etc/libvirt/qemu.conf'
        'etc/libvirt/qemu/networks/autostart/default.xml'
        'etc/libvirt/qemu/networks/default.xml'
        'etc/libvirt/virt-login-shell.conf'
        'etc/libvirt/virtlockd.conf'
        'etc/logrotate.d/libvirtd'
        'etc/logrotate.d/libvirtd.lxc'
        'etc/logrotate.d/libvirtd.qemu'
        'etc/logrotate.d/libvirtd.uml'
        'etc/sasl2/libvirt.conf')
install="${pkgname/-git}.install"
source=("${pkgname}::git://libvirt.org/${pkgname/-/.}#tag=v${pkgver}"
        libvirtd.conf.d
        libvirtd-guests.conf.d
        "${pkgname/-git}.tmpfiles.d"
        "${pkgname/-git}.install"
        )
sha512sums=('SKIP'
            'eebc616fcdf3c92338f3967f99701ed2d156d4a3835c31aebe8299b2f5fe00717f09ee2da79b3d5810348033f0b82ff05c70a775a8309d886919e75fa862e302'
            '9012a2bd2b262a6f95d739f97b6ebc6938310616aca05d501efd9d3fc69d71500366dcfe9046a37f8ce30929bcd6eea9cf6770cf757998ecce4b475a25914801'
            '9e3e9f479b17528d70e812e6851cf4209ad73b577d478170bf7d80f075be4f2ee2e28adbe1e542002bbaea15e34830ff60e4ca2e34a5ac5a925440ca25864c31'
            'ecdc54848488253745733d57206c784455fd6747da88b0ddd27a7476ccb50b30381815552c0f32241ff2dc83fa200d32f2fae2c01d303dd9e6a600f8837f4f82')

pkgver() {
  cd "${pkgname}"

  #git describe --always | sed 's|-|.|g' | sed 's/^.//'
  echo "${_pkgver}.r$(git rev-list --count master).g$(git log -1 --format="%h")"
}

prepare() {
  cd "${pkgname}"

  for file in $(find . -name '*.py' -print); do
    sed -i 's_#!.*/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i 's_#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done

  sed -i 's|/sysconfig/|/conf.d/|g' \
          daemon/libvirtd.service.in \
          tools/{libvirt-guests.service,libvirt-guests.sh,virt-pki-validate}.in \
          src/locking/virtlockd.service.in
  sed -i 's|@sbindir@|/usr/bin|g' \
          src/locking/virtlockd.service.in
  sed -i 's|#group =.*|group="kvm"|' \
          src/qemu/qemu.conf
  sed -i 's|/usr/libexec/qemu-bridge-helper|/usr/lib/qemu/qemu-bridge-helper|g' \
          src/qemu/qemu{.conf,_conf.c} \
          src/qemu/test_libvirtd_qemu.aug.in
}

build() {
  cd "${pkgname}"

  export PYTHON=`which python2`
  export LDFLAGS="${LDFLAGS} -lX11"
  export RADVD=/usr/bin/radvd
  NOCONFIGURE=1 ./autogen.sh
  [ -f Makefile ] || ./configure \
                        --prefix=/usr \
                        --libexec=/usr/lib/"${pkgname/-git}" \
                        --sbindir=/usr/bin \
                        --with-storage-lvm \
                        --without-xen \
                        --with-udev \
                        --without-hal \
                        --disable-static \
                        --with-init-script=systemd \
                        --with-qemu-user=nobody \
                        --with-qemu-group=nobody \
                        --without-netcf \
                        --with-interface \
                        --with-lxc \
                        --with-storage-iscsi \
                        --with-numad
  make
}

package() {
  cd "${pkgname}"

  make DESTDIR="${pkgdir}" install

  install -Dm644 ../libvirtd.conf.d "${pkgdir}/etc/conf.d/libvirtd"
  install -Dm644 ../libvirtd-guests.conf.d "${pkgdir}/etc/conf.d/libvirt-guests"

  # systemd stuff
  install -Dm644 ../libvirt.tmpfiles.d "${pkgdir}/usr/lib/tmpfiles.d/libvirt.conf"

  chown -R 0:78 "${pkgdir}/var/lib/libvirt/qemu/channel/target/"
  chmod 0770 "${pkgdir}/var/lib/libvirt/qemu/channel/target/"

  rm -rf \
    "${pkgdir}/var/run" \
    "${pkgdir}/etc/sysconfig" \
    "${pkgdir}/etc/rc.d"
}

# vim:set ts=2 sw=2 et:
